/*F.Hecht, G.Lance, E. TrÃ©lat
Reusable template to solve optimization problem using IpOpt within FreeFEM
Example taken from "PDE constrained optimization within FreeFEM"
Example (2.1,2.2,2.3) in Section 2.1.4:
Solving template for LQ example using AMPL
\min_{u} \int_{\Omega} (y-y_d)^2\,dx + \alpha \int_{\Omega} u^2 \,dx
under the constraints
-\Delta y = u, y_{\vert \partial \Omega} = 0, y(0)= 0
Use of AMPL solver -> need to export matrices and array built with FFEM
*/

verbosity = 0;

// mesh
mesh Th = square(50,50,flags=0);

// u_m \leq u \leq u_M
real xm = -10000;
real xM = 10000;
real um = 0;
real uM = 1;

real alpha = 1.0;
fespace Vh(Th,P1);
int nbs = Vh.ndof;

cout << nbs << endl;

Vh yh,vh,ydfe=1;

// Variational formulations
macro grad(yh) [dx(yh), dy(yh)] //

varf vA(yh,vh) = int2d(Th)( grad(yh)'*grad(vh) )
			 + on(1,2,3,4,yh=0); // Dirichlet laplacian
varf vB(yh,vh) = int2d(Th)(yh*vh); // mass
varf vL(yh,vh) = int2d(Th)(vh); // volume

matrix A = vA(Vh,Vh);
matrix B = vB(Vh,Vh);
macro  export(fn,A)
{
	ofstream fout(fn+".txt");
	fout << A << endl; 
	}// EOF Macro 

// Target
real[int] yd = ydfe[];
real[int] L = vL(0,Vh);

// Export data
export("A",A)
export("B",B)
export("yd",yd)
export("y",yh[])
export("ndof",nbs)

{ofstream fout("L.txt");
 fout << nbs << endl;
 for (int i=0;i<nbs;i++)
 {
	fout << i  << " " << L[i] << endl;
	}}

// lecture fichier test.py
{
ifstream f("y.txt");
f >> yh[];
};

plot(Th,yh,wait=1,fill=1);



